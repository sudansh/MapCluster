apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'com.sudansh.deliveries.testing.OpenForTesting'
}

android {
    compileSdkVersion 28
    dataBinding.enabled = true
    defaultConfig {
        applicationId "com.sudansh.deliveries"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    androidExtensions {
        experimental = true
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }
    lintOptions {
        abortOnError false
    }

}
ext {
    supportLib = '28.0.0'
    koin_version = '0.9.3'
    retrofit = '2.4.0'
    room = '1.1.1'
    glide = '4.8.0'
    mockito = "2.7.19"
    lifecycle = "1.1.1"
    espresso = '3.0.1'
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    //android arch
    implementation "android.arch.lifecycle:extensions:$lifecycle"
    testImplementation "android.arch.core:core-testing:$lifecycle"
    androidTestImplementation "android.arch.core:core-testing:$lifecycle"

    //map
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    //android support
    implementation "com.android.support:appcompat-v7:$supportLib"
    implementation "com.android.support:recyclerview-v7:$supportLib"
    implementation "com.android.support:design:$supportLib"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    //room
    implementation "android.arch.persistence.room:runtime:$room"
    kapt "android.arch.persistence.room:compiler:$room"

    //glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation 'com.google.code.gson:gson:2.8.5'

    //Koin
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-architecture:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"
    androidTestImplementation "org.koin:koin-test:$koin_version"

    //mockito
    testImplementation "org.mockito:mockito-core:$mockito"
    androidTestImplementation "org.mockito:mockito-android:$mockito"
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.11.0'
    testImplementation 'junit:junit:4.12'

    //espresso
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espresso"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'
}
